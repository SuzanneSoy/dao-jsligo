#import "../../src/main.jsligo" "DAO"
#import "./assert.jsligo" "Assert"

import Lambda = DAO.Lambda;
import Outcome = DAO.Outcome;

// Some types for readability
type taddr = typed_address<DAO.parameter, DAO.storage>;
type contr = contract<DAO.parameter>;
export type originated = {
    addr: address,
    taddr: taddr,
    contr: contr,
};

// Some dummy values intended to be used as placeholders
const dummyPacked = Bytes.pack (OperationList((() => list([]) as list<operation>)) as Lambda.t);
const dummyHash = Crypto.sha256 (dummyPacked);

const dummyProposal : DAO.Proposal.makeParams = {
    descriptionLink: "ipfs://QmbKq7QriWWU74NSq35sDSgUf24bYWTgpBq3Lea7A3d7jU",
    hash: Some(dummyHash)
};
const dummyGovernanceToken = "KT1VqarPDicMFn1ejmQqqshUkUXTCTXwmkCN" as address;

// Some default values
const emptyVault: DAO.Storage.Vault.t = Big_map.empty;
const emptyOutcomes: DAO.Storage.outcomes = Big_map.empty;

const baseConfig : DAO.Storage.Config.t = {
    depositAmount: 4 as nat,
    refundThreshold: 32 as nat,
    quorumThreshold: 67 as nat,
    superMajority: 80 as nat,
    startDelay: 360 as nat,
    votingPeriod: 1440 as nat,
    timelockDelay: 360 as nat,
    timelockPeriod: 720 as nat,
    burnAddress: "tz1burnburnburnburnburnburnburjAYjjX" as address
};

const baseStorage : DAO.storage = {
    metadata: Big_map.literal (list([
        ["", Bytes.pack("tezos-storage:contents")],
        ["contents", "" as bytes]
    ])),
    governanceToken: dummyGovernanceToken,
    vault: emptyVault,
    proposal: None() as option<DAO.Proposal.t>,
    config: baseConfig,
    nextOutcomeId: 1 as nat,
    outcomes: emptyOutcomes
};

// Originate a DAO contract with given init_storage storage
const originate = (init_storage: DAO.storage) : originated => {
    const [taddr, _, _] = Test.originate (DAO.main, init_storage, 0 as tez);
    const contr = Test.to_contract (taddr);
    const addr = Tezos.address (contr);
    return { addr: addr, taddr: taddr, contr: contr };
};

// Call entry point of DAO contr contract
const call = ([p, contr] : [DAO.parameter, contr]): test_exec_result =>
    Test.transfer_to_contract (contr, p, 0 as tez);

// Entry points call helpers
const cancel = ([outcome_key_opt, contr] : [option<nat>, contr]): test_exec_result =>
    call(Cancel(outcome_key_opt), contr);

const endVote = (contr: contr): test_exec_result => call(EndVote(), contr);

const execute = ([k, packed, contr] : [nat, bytes, contr]): test_exec_result =>
    call(Execute({ outcomeKey: k, packed: packed }), contr);

const lock = ([amount_, contr]: [nat, contr]): test_exec_result => call(Lock(amount_), contr);

const propose = ([proposal, contr] : [DAO.Proposal.makeParams, contr]): test_exec_result =>
    call(Propose(proposal), contr);

const release = ([amount_, contr]: [nat, contr]): test_exec_result =>
    call(Release(amount_), contr);

const vote = ([choice, contr]: [bool, contr]): test_exec_result => call(Vote(choice), contr);

// Asserter helper for successful entry point calls
const cancelSuccess = ([outcome_key_opt, contr] : [option<nat>, contr]): unit =>
    Assert.txSuccess(cancel(outcome_key_opt, contr));

const endVoteSuccess = (contr : contr): unit =>
    Assert.txSuccess(endVote(contr));

const executeSuccess = ([k, packed, contr] : [nat, bytes, contr]): unit =>
    Assert.txSuccess(execute(k, packed, contr));

const lockSuccess = ([amount_, contr]: [nat, contr]): unit =>
    Assert.txSuccess (lock(amount_, contr));

const proposeSuccess = ([proposal, contr] : [DAO.Proposal.makeParams, contr]): unit =>
    Assert.txSuccess(propose(proposal, contr));

const releaseSuccess = ([amount_, contr]: [nat, contr]): unit =>
    Assert.txSuccess(release(amount_, contr));

const voteSuccess = ([choice, contr]: [DAO.Vote.choice, contr]): unit =>
    Assert.txSuccess(vote(choice, contr));

// Batch call of lock entry point, WARNING: changes Test framework source
const batchLock = ([addr_lst, amount_, contr] : [list<address>, nat, contr]) : unit => {
    const lock = (addr: address) : unit => {
        Test.set_source (addr);
        lockSuccess(amount_, contr);
    };
    List.iter(lock, addr_lst);
};

// Batch call of vote entry point, WARNING: changes Test framework source
const batchVote = ([addr_lst, choice, contr] : [list<address>, DAO.Vote.choice, contr]) : unit => {
    const vote = (addr: address) : unit => {
        Test.set_source (addr);
        voteSuccess(choice, contr);
    };
    List.iter(vote, addr_lst);
};

// Assert DAO contract at [taddr] has [owner] [amount_] of tokens locked
const assertLocked = ([taddr, owner, amount_] : [taddr, DAO.Storage.Vault.owner,  nat]) : unit => {
    const s = Test.get_storage(taddr);
    match (Big_map.find_opt(owner, s.vault), {
        Some: (tokens: nat) => assert(tokens == amount_),
        None: () => Test.failwith("Big_map key should not be missing")
    });
};

// Assert DAO contract at [taddr] have registered [voter] [choice] with [amount_] votes
const assertVoted = ([taddr, voter, choice, amount_] : [taddr, address, bool, nat])
: unit => {
    const s = Test.get_storage (taddr);
    const p = Option.unopt(s.proposal);
    match (Map.find_opt(voter, p.votes), {
        Some: (vote: DAO.Vote.t) => assert(vote[0] == choice && vote[1] == amount_),
        None: () => Test.failwith("Map key should not be missing")
    });
};

// Assert DAO contract at [taddr] have an outcome occuring for [n] key in Executed state
const assertExecuted = ([taddr, n]: [taddr, nat]) : unit => {
    const s = Test.get_storage (taddr);
    match (Big_map.find_opt(n, s.outcomes), {
        None: () => Test.failwith("The outcome should exists"),
        Some: (o: Outcome.t) => {
            const [_, state] = o;
            assert(state == (Executed() as Outcome.state))
        }
    });
};

// Assert outcomes [bm] big map have an entry for key [k] and has given [s] state
const assertProposalState =
    ([bm, k, s]: [DAO.Storage.outcomes, nat, DAO.Outcome.state]) : unit =>
    match(Big_map.find_opt(k, bm), {
        Some: (outcome: DAO.Outcome.t) => {
            const [proposal, state] = outcome;
            // just checking timelock existence
            const _checkTimelock = proposal.timelock;
            // check that the proposal have been accepted
            assert(state == s)
        },
        None: () => Test.failwith("outcome not found")
    });
